#ifndef CAR_H
#define CAR_H

#include "Motor.h"
#include <Servo.h>

static camera_config_t camera_config = {
    .pin_pwdn = PWDN_GPIO_NUM,
    .pin_reset = RESET_GPIO_NUM,
    .pin_xclk = XCLK_GPIO_NUM,
    .pin_sscb_sda = SIOD_GPIO_NUM,
    .pin_sscb_scl = SIOC_GPIO_NUM,
    .pin_d7 = Y9_GPIO_NUM,
    .pin_d6 = Y8_GPIO_NUM,
    .pin_d5 = Y7_GPIO_NUM,
    .pin_d4 = Y6_GPIO_NUM,
    .pin_d3 = Y5_GPIO_NUM,
    .pin_d2 = Y4_GPIO_NUM,
    .pin_d1 = Y3_GPIO_NUM,
    .pin_d0 = Y2_GPIO_NUM,
    .pin_vsync = VSYNC_GPIO_NUM,
    .pin_href = HREF_GPIO_NUM,
    .pin_pclk = PCLK_GPIO_NUM,
    .xclk_freq_hz = 19000000,
    .ledc_timer = LEDC_TIMER_0,
    .ledc_channel = LEDC_CHANNEL_0,
    .pixel_format = PIXFORMAT_JPEG,
    .frame_size = FRAMESIZE_UXGA,
    .jpeg_quality = 10,
    .fb_count = 2,
    .fb_location = CAMERA_FB_IN_PSRAM,
    .grab_mode = CAMERA_GRAB_LATEST};

class Car {
public:
  Car()
      : isFlashOn(false),
        currentAngleX(90),
        targetAngleX(90),
        lastUpdate(0),
        lastCommandTime(0),
        motorStopped(true),
        motorL(LEFT_MOTOR_IN1, LEFT_MOTOR_IN2, LEFT_MOTOR_PWM_CHANNEL_1, LEFT_MOTOR_PWM_CHANNEL_2),
        motorR(RIGHT_MOTOR_IN1, RIGHT_MOTOR_IN2, RIGHT_MOTOR_PWM_CHANNEL_1, RIGHT_MOTOR_PWM_CHANNEL_2) {}

  esp_err_t init() {
    pinMode(FLASH_PIN, OUTPUT);
    digitalWrite(FLASH_PIN, LOW);

    initMotors();

    bool res = servoX.attach(SERVO_X_PIN, 6);
    Serial.printf("Servo X attach result: %s\n", res ? "SUCCESS" : "FAILURE");
    servoX.write(90);
    delay(100);

    lastCommandTime = nowMs();

    return initCamera();
  }

  void onCommand() {
    lastCommandTime = nowMs();
    motorStopped = false;
  }

  void tick() {
    updateServo();
    tickAutoStop();
    motorL.tick();
    motorR.tick();
  }

  void toggleFlash() {
    isFlashOn = !isFlashOn;
    digitalWrite(FLASH_PIN, isFlashOn ? HIGH : LOW);
  }

  void turnFlashOff() {
    isFlashOn = false;
    digitalWrite(FLASH_PIN, LOW);
  }

  bool getFlashState() {
    return isFlashOn;
  }

  void moveForward() {
    onCommand();
    motorL.moveForward(motorMax);
    motorR.moveForward(motorMax);
  }

  void moveBackward() {
    onCommand();
    motorL.moveBackward(motorMax);
    motorR.moveBackward(motorMax);
  }

  void turnRight() {
    onCommand();
    motorL.moveForward(motorMax);
    motorR.moveBackward(motorMax);
  }

  void turnLeft() {
    onCommand();
    motorL.moveBackward(motorMax);
    motorR.moveForward(motorMax);
  }

  void moveForwardLeft() {
    onCommand();
    motorL.moveForward(motorMax / 1.5);
    motorR.moveForward(motorMax);
  }

  void moveForwardRight() {
    onCommand();
    motorL.moveForward(motorMax);
    motorR.moveForward(motorMax / 1.5);
  }

  void moveBackwardLeft() {
    onCommand();
    motorL.moveBackward(motorMax / 1.5);
    motorR.moveBackward(motorMax);
  }

  void moveBackwardRight() {
    onCommand();
    motorL.moveBackward(motorMax);
    motorR.moveBackward(motorMax / 1.5);
  }

  void stop() {
    motorL.stop();
    motorR.stop();
    motorStopped = true;
  }

  void setCameraX(int x) {
    x = constrain(x, -100, 100);
    targetAngleX = map(x, -100, 100, 0, 180);
  }

private:
  bool isFlashOn;
  Servo servoX;

  int currentAngleX;
  int targetAngleX;
  uint64_t lastUpdate;

  uint8_t motorMax = 255;
  Motor motorL;
  Motor motorR;

  uint64_t lastCommandTime;
  bool motorStopped;

  void updateServo() {
    uint64_t now = nowMs();
    const int stepDelay = 40;
    
    if (now - lastUpdate < stepDelay) {
      return;
    }

    lastUpdate = now;

    if (currentAngleX == targetAngleX) {
      return;
    }

    const int step = 2;
    
    if (currentAngleX < targetAngleX) {
      currentAngleX += step;
      if (currentAngleX > targetAngleX) {
        currentAngleX = targetAngleX;
      }
    } else {
      currentAngleX -= step;
      if (currentAngleX < targetAngleX) {
        currentAngleX = targetAngleX;
      }
    }

    servoX.write(currentAngleX);
  }

  void tickAutoStop() {
    const uint64_t AUTOSTOP_TIMEOUT_MS = 500;
    uint64_t now = nowMs();

    int64_t diff = elapsedSince(lastCommandTime);

    if (!motorStopped && (uint64_t)diff > AUTOSTOP_TIMEOUT_MS) {
      stop();
      Serial.println("[AutoStop] No command for 500ms, stopping motors");
    }
  }

  esp_err_t initCamera() {
    esp_err_t err = esp_camera_init(&camera_config);

    if (err != ESP_OK) {
      Serial.printf("Camera error: 0x%x\n", err);
      return err;
    }

    sensor_t *s = esp_camera_sensor_get();
    
    if (!s) {
      Serial.println("NO SENSOR DETECTED");
      return ESP_FAIL;
    }

    s->set_framesize(s, FRAMESIZE_VGA);
    delay(100);

    Serial.println("Camera initialized");
    return ESP_OK;
  }

  void initMotors() {
    motorL.setMinPwm(200);
    motorR.setMinPwm(200);

    motorL.begin();
    motorR.begin();
  }
};

#endif